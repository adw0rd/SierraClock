#include <GyverBME280.h>
GyverBME280 bme;

#include <SPI.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SH110X.h>

/* Uncomment the initialize the I2C address , uncomment only one, If you get a totally blank screen try the other*/
#define i2c_Address 0x3c //initialize with the I2C addr 0x3C Typically eBay OLED's
// #define i2c_Address 0x3d //initialize with the I2C addr 0x3D Typically Adafruit OLED's

#define SCREEN_WIDTH 128 // OLED display width, in pixels
#define SCREEN_HEIGHT 64 // OLED display height, in pixels
#define OLED_RESET -1   //   QT-PY / XIAO
Adafruit_SH1106G display = Adafruit_SH1106G(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

// Frame offset (bezel covers edge pixels)
#define FRAME_OFFSET 1
#define FRAME_TOP 10
#define FRAME_BOTTOM 8

// Buttons
#define BTN1_PIN 2  // Button 1 (reserved for future use)
#define BTN2_PIN 3  // Button 2 - menu navigation

// Menu state
int currentApp = 0;  // 0=Clock, 1=Weather, 2=About, 3=Logo
const int APP_COUNT = 4;
unsigned long lastButtonPress = 0;
const unsigned long debounceDelay = 200;

// Time editing state
bool editingTime = false;
int editingField = 0;  // 0=hours, 1=minutes


// Manual time setting
int manualHours = 0;
int manualMinutes = 0;
unsigned long timeOffset = 0;  // offset from millis() start


// SIERRA logo bitmap 128x32 pixels
#define SIERRA_LOGO_WIDTH  128
#define SIERRA_LOGO_HEIGHT 32
static const unsigned char PROGMEM sierra_logo[] = {
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff,
  0xfc, 0x00, 0x00, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0x00, 0x0f, 0x80, 0x00, 0x1e, 0x00, 0x1f, 0xff,
  0xf8, 0x00, 0x00, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0x00, 0x07, 0x80, 0x00, 0x0e, 0x00, 0x0f, 0xff,
  0xf0, 0x00, 0x00, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0x00, 0x03, 0x80, 0x00, 0x0e, 0x00, 0x0f, 0xff,
  0xf0, 0x00, 0x00, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0x00, 0x03, 0x80, 0x00, 0x06, 0x00, 0x07, 0xff,
  0xf0, 0x00, 0x00, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0x00, 0x03, 0x80, 0x00, 0x06, 0x0e, 0x07, 0xff,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0x01, 0xff, 0xfe, 0x07, 0xff, 0xff, 0xff,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0x83, 0xff, 0xfe, 0x07, 0xff, 0xff, 0xff,
  0xf0, 0x00, 0x1f, 0xe0, 0xe0, 0x00, 0x01, 0xc0, 0x00, 0x03, 0x80, 0x00, 0x06, 0x00, 0x01, 0xff,
  0xf0, 0x00, 0x03, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0x00, 0x03, 0x80, 0x00, 0x0e, 0x00, 0x01, 0xff,
  0xf8, 0x00, 0x01, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0x00, 0x07, 0x80, 0x00, 0x0e, 0x00, 0x00, 0xff,
  0xf8, 0x00, 0x00, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0x00, 0x0f, 0x80, 0x00, 0x1e, 0x00, 0x00, 0xff,
  0xff, 0x00, 0x00, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0x00, 0x1f, 0x80, 0x00, 0x7e, 0x00, 0x00, 0x7f,
  0xff, 0xff, 0xc0, 0xe0, 0x60, 0x1f, 0xff, 0xc0, 0xf0, 0x3f, 0x81, 0xe0, 0x7e, 0x0f, 0xe0, 0x7f,
  0xff, 0xff, 0xc0, 0xe0, 0x60, 0x1f, 0xff, 0xc0, 0xf8, 0x1f, 0x81, 0xe0, 0x7e, 0x0f, 0xe0, 0x3f,
  0xff, 0xff, 0x80, 0xe0, 0x60, 0x0f, 0xff, 0xc0, 0xf8, 0x1f, 0x81, 0xe0, 0x3e, 0x0f, 0xf0, 0x3f,
  0xf0, 0x00, 0x00, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0xf8, 0x1f, 0x81, 0xf0, 0x3e, 0x0f, 0xf0, 0x1f,
  0xf0, 0x00, 0x00, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0xf8, 0x0f, 0x81, 0xf0, 0x3e, 0x0f, 0xf8, 0x1f,
  0xf0, 0x00, 0x01, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0xfc, 0x0f, 0x81, 0xf0, 0x1e, 0x0f, 0xf8, 0x0f,
  0xf0, 0x00, 0x03, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0xfc, 0x0f, 0x81, 0xf8, 0x1e, 0x0f, 0xfc, 0x0f,
  0xf0, 0x00, 0x0f, 0xe0, 0x60, 0x00, 0x01, 0xc0, 0xfc, 0x07, 0x81, 0xf8, 0x1e, 0x0f, 0xfc, 0x0f,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff,
  0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff,
  0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
  0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
  0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
  0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00
};


void setup() {
  Serial.begin(9600);

  // Setup buttons with internal pull-up resistors
  pinMode(BTN1_PIN, INPUT_PULLUP);
  pinMode(BTN2_PIN, INPUT_PULLUP);

  delay(250); // wait for the OLED to power up
  display.begin(i2c_Address, true); // Address 0x3C default
  // display.setContrast (0); // dim display

  // Show SIERRA logo on startup for 5 seconds
  display.clearDisplay();
  display.drawBitmap(0, 16, sierra_logo, SIERRA_LOGO_WIDTH, SIERRA_LOGO_HEIGHT, SH110X_WHITE);
  display.display();
  delay(5000);
  display.clearDisplay();

  Serial.println("Start");
  if (!bme.begin(0x76)) Serial.println("Error!");
}


void loop() {
  bool btn1 = digitalRead(BTN1_PIN) == LOW;
  bool btn2 = digitalRead(BTN2_PIN) == LOW;
  bool canPress = (millis() - lastButtonPress > debounceDelay);

  // BTN1 - OK: enter/increment time (on Clock screen)
  if (btn1 && canPress) {
    lastButtonPress = millis();
    if (currentApp == 0) {
      if (!editingTime) {
        editingTime = true;
        editingField = 0;
      } else if (editingField == 0) {
        manualHours = (manualHours + 1) % 24;
        updateTimeOffset();
      } else {
        manualMinutes = (manualMinutes + 1) % 60;
        updateTimeOffset();
      }
    }
  }

  // BTN2 - NEXT: navigate / switch field
  if (btn2 && canPress) {
    lastButtonPress = millis();
    if (currentApp == 0 && editingTime) {
      if (editingField == 0) {
        editingField = 1;
      } else {
        editingTime = false;
      }
    } else {
      currentApp = (currentApp + 1) % APP_COUNT;
      editingTime = false;
    }
  }

  display.clearDisplay();

  switch (currentApp) {
    case 0: showClock(); break;
    case 1: showWeather(); break;
    case 2: showAbout(); break;
    case 3: showLogo(); break;
  }

  display.display();
  delay(100);
}

void updateTimeOffset() {
  timeOffset = (unsigned long)manualHours * 3600000UL + (unsigned long)manualMinutes * 60000UL - millis();
}

// Draw frame border
void drawFrame() {
  display.drawRect(0, FRAME_TOP, SCREEN_WIDTH, SCREEN_HEIGHT - FRAME_TOP - FRAME_BOTTOM, SH110X_WHITE);
}

// App 0: Clock
void showClock() {
  unsigned long totalMs = millis() + timeOffset;
  int hours = (totalMs / 3600000UL) % 24;
  int minutes = (totalMs / 60000UL) % 60;

  display.setTextColor(SH110X_WHITE);

  if (editingTime) {
    display.setTextSize(1);
    display.setCursor(40, 10);
    display.print("SET TIME");
    display.setTextSize(2);
    display.setCursor(10, 30);
    if (editingField == 0) display.print("["); else display.print(" ");
    if (manualHours < 10) display.print("0");
    display.print(manualHours);
    if (editingField == 0) display.print("]"); else display.print(" ");
    display.print(":");
    if (editingField == 1) display.print("["); else display.print(" ");
    if (manualMinutes < 10) display.print("0");
    display.print(manualMinutes);
    if (editingField == 1) display.print("]");
  } else {
    display.setTextSize(4);
    display.setCursor(4, FRAME_TOP + 7);
    if (hours < 10) display.print("0");
    display.print(hours);
    display.print(":");
    if (minutes < 10) display.print("0");
    display.print(minutes);
  }
}

// App 1: Temperature & Humidity
void showWeather() {

  display.setTextColor(SH110X_WHITE);
  display.setTextSize(2);
  display.setCursor(2 + FRAME_OFFSET, FRAME_TOP + 5);
  display.print("T: ");
  display.print(bme.readTemperature(), 1);
  display.println(" C");

  display.setCursor(2 + FRAME_OFFSET, FRAME_TOP + 25);
  display.print("H: ");
  display.print(bme.readHumidity(), 1);
  display.println(" %");
}

// App 2: About
void showAbout() {
  display.setTextColor(SH110X_WHITE);
  display.setTextSize(2);
  display.setCursor(30, 15);
  display.print("adw0rd");
  display.setCursor(20, 40);
  display.print("(c) 2025");
}

// App 4: Logo
void showLogo() {
  display.drawBitmap(0, 16, sierra_logo, SIERRA_LOGO_WIDTH, SIERRA_LOGO_HEIGHT, SH110X_WHITE);
}
